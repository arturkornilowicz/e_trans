:: Number e is Transcendental
::  by Tomasz Korni{\l}owicz

environ

 vocabularies NUMBERS, SUBSET_1, CARD_1, ARYTM_1, TARSKI, ARYTM_3, REAL_1,
      NAT_1, INT_1, XCMPLX_0, LIOUVIL1, COMPLEX1, NEWTON, IRRAT_1, FUNCT_7,
      FINSET_1, FUNCT_1, CARD_3, ALGNUM_1, COMPLFLD, XBOOLE_0, POLYNOM1,
      RELAT_1, VECTSP_1, POLYNOM5, XXREAL_0, FINSEQ_1, GAUSSINT, RATFUNC1,
      MESFUNC1, SUPINF_2, POLYNOM2, C0SP1, FUNCSDOM, STRUCT_0, POLYNOM3,
      VALUED_0, INT_3, ALGSEQ_1, GROUP_1, PARTFUN1, CAT_1, ALGSTR_0, MSAFREE2,
      XXREAL_1, ORDINAL2, XXREAL_2, FDIFF_1, MEASURE5, ORDINAL4, FINSEQ_2,
      BINOP_2, SETWISEO, WAYBEL_8, POWER, E, POLYDIFF, SERIES_1, RLVECT_1,
      LATTICES, SIN_COS, VALUED_1, MEMBERED, FUNCOP_1, SQUARE_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, FINSET_1, RELAT_1, FUNCT_1, RELSET_1,
      PARTFUN1, FUNCT_2,
      FINSEQ_1, NUMBERS, XCMPLX_0, COMPLEX1, XREAL_0, NAT_1, NAT_D,
      FINSEQ_2, BINOP_2, SETWOP_2, XXREAL_0, XXREAL_2, RCOMP_1, SQUARE_1,
      MEMBERED, FUNCOP_1, RFUNCT_1, VALUED_0,
      VALUED_1, INT_1, POWER,
      NEWTON, RAT_1, COMSEQ_2, MEASURE5, IRRAT_1, FCONT_1,
      FDIFF_1, RVSUM_1, SIN_COS, STRUCT_0, ALGSTR_0,
      RLVECT_1, GROUP_1, GROUP_4, VECTSP_1, C0SP1,
      INT_3, COMPLFLD, GAUSSINT, ALGSEQ_1, POLYNOM3, POLYNOM4, POLYNOM5,
      UPROOTS, RATFUNC1, RING_4, ALGNUM_1, POLYDIFF, FVSUM_1, LIOUVIL1;
 constructors NEWTON, ALGNUM_1, ALGSEQ_1, C0SP1, POLYNOM4, FUNCT_7, TOPMETR,
      EC_PF_1, BINOP_2, NAT_D, RATFUNC1, RCOMP_1, UPROOTS, GR_CY_1, FVSUM_1,
      GAUSSINT, REALSET1, POLYDIFF, XXREAL_2, FCONT_1, MEASURE5, COMSEQ_2,
      GROUP_4, FINSOP_1, ALGSTR_1, LIOUVIL1, POWER, SIN_COS, RFUNCT_1,
      SQUARE_1;
 registrations XREAL_0, RELAT_1, ORDINAL1, RAT_1, INT_1, XXREAL_0, INT_6,
      VECTSP_1, POLYNOM3, STRUCT_0, MEMBERED, RELSET_1, POLYNOM5, RING_4,
      XCMPLX_0, NUMBERS, XBOOLE_0, NAT_1, VALUED_0, RATFUNC1, FINSEQ_1, NEWTON,
      FINSEQ_2, WSIERP_1, ABSVALUE, NIVEN, COMPLFLD, ALGSTR_1, POLYNOM4,
      GAUSSINT, INT_3, SUBSET_1, NEWTON04, UPROOTS, POLYDIFF, XXREAL_1,
      XXREAL_2, FINSET_1, FCONT_1, COMPLEX1, RCOMP_1, IDEAL_1, RFUNCT_1,
      PREPOWER, LIOUVIL1, ALGNUM_1, LIOUVIL2, VALUED_1, PARTFUN1, FUNCT_2,
      FDIFF_1, FUNCOP_1, SQUARE_1, PARTFUN3;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, RELAT_1, FUNCT_1, FUNCT_2,
      STRUCT_0, ALGSEQ_1, C0SP1, POLYNOM5, RATFUNC1,
      ALGNUM_1, MEASURE5, POLYDIFF;
 equalities STRUCT_0, VECTSP_1, GAUSSINT, RATFUNC1, POLYNOM3, INT_3, COMPLEX1,
      REALSET1, POLYDIFF, XCMPLX_0, SIN_COS, VALUED_1;
 expansions TARSKI, ALGNUM_1, UPROOTS, ALGSEQ_1, RATFUNC1, POLYNOM5, FDIFF_1,
      INT_1, POLYDIFF;
 theorems XREAL_0, NEWTON, XXREAL_0, XREAL_1, XCMPLX_1, NAT_1, PREPOWER,
      ALGNUM_1, COMPLFLD, RING_3, POLYNOM4, UPROOTS, INT_1, GAUSSINT, C0SP1,
      FUNCT_2, ALGSEQ_1, RLVECT_1, FINSEQ_3, FINSEQ_2, ZFMISC_1, FUNCT_7,
      RATFUNC1, RELAT_1, FINSEQ_1, FUNCT_1, POLYDIFF, XXREAL_2, ROLLE,
      COMPLEX1, POLYNOM5, XXREAL_1, XBOOLE_0, TARSKI, FDIFF_1, ABSVALUE,
      VALUED_1, INTEGRA5, INTEGRA1, RAT_1, NAT_3, FVSUM_1, NIVEN, NAT_D,
      RVSUM_1, INT_2, XCMPLX_0, NUMBERS, RING_2, GROUP_4, LIOUVIL1, LIOUVIL2,
      POWER, POLYNOM3, IRRAT_1, SIN_COS, FDIFF_2, RFUNCT_1, XBOOLE_1, SQUARE_1,
      VALUED_2;
 schemes FRAENKEL, FINSEQ_1, FINSEQ_2, POLYNOM3, RECDEF_1;

begin :: Preliminaries

reserve m,n for Nat;
reserve r for Real;

theorem T3:
  for R being non-empty Relation holds R"{{}} = {}
  proof
    let R be non-empty Relation;
    thus R"{{}} c= {}
    proof
      let x be object;
      assume x in R"{{}};
      then consider y being object such that
A1:   y in rng R and
      [x,y] in R and
A2:   y in {{}} by RELAT_1:131;
      y = {} by A2,TARSKI:def 1;
      hence thesis by A1,RELAT_1:def 9;
    end;
    thus thesis;
  end;

registration
  cluster non-empty differentiable for Function of REAL,REAL;
  existence
  proof
    take REAL-->In(1,REAL);
    thus thesis;
  end;
end;

registration
  let f be non-empty Function of REAL,REAL;
  cluster f^2 -> non-empty;
  coherence;
end;

theorem T4:
  for f being non-empty Function of REAL, REAL holds f.r <> 0
  proof
    let f be non-empty Function of REAL,REAL;
    dom f = REAL by FUNCT_2:def 1;
    then f.r in rng f by XREAL_0:def 1,FUNCT_1:3;
    hence f.r <> 0 by RELAT_1:def 9;
  end;

theorem T2a:
  for f being non-empty complex-valued Function holds f" = f^
  proof
    let f be non-empty complex-valued Function;
    thus
A1: dom(f") = dom f \ f"{0} by VALUED_1:def 7
    .= dom(f^) by RFUNCT_1:def 2;
    let x be object;
    assume x in dom(f");
    hence (f^).x = (f.x)" by A1,RFUNCT_1:def 2
    .= (f").x by VALUED_1:10;
  end;

theorem T2:
  for f being complex-valued Function
  for g being non-empty complex-valued Function holds
  f /" g = f / g
  proof
    let f be complex-valued Function;
    let g be non-empty complex-valued Function;
    thus
A1: dom(f/"g) = dom f /\ (dom g \ g"{0}) by VALUED_1:16
    .= dom(f/g) by RFUNCT_1:def 1;
    let x be object;
    assume x in dom(f/"g);
    hence (f/g).x = f.x / g.x by A1,RFUNCT_1:def 1
    .= (f/"g).x by VALUED_1:17;
  end;

theorem T6:
  for f, g, h being complex-valued Function
  st dom f c= dom h & dom g c= dom h & h is non-empty
  holds f/"g = (f(#)h)/"(g(#)h)
  proof
    let f, g, h be complex-valued Function;
    assume
A0: dom f c= dom h;
    then
A1: dom f /\ dom h = dom f by XBOOLE_1:28;
    assume dom g c= dom h;
    then
A2: dom g /\ dom h = dom g by XBOOLE_1:28;
A3: dom(f(#)h) = dom f /\ dom h by VALUED_1:def 4;
A4: dom(g(#)h) = dom g /\ dom h by VALUED_1:def 4;
    assume
A8: h is non-empty;
A9: dom(f/"g) = dom(f) /\ dom (g) by VALUED_1:16;
    hence dom(f/"g) = dom((f(#)h)/"(g(#)h)) by A1, A2, A3, A4, VALUED_1:16;
    let x be object;
    assume
A10: x in dom(f/"g);
A5: (f(#)h).x = f.x * h.x by VALUED_1:5;
A6: (g(#)h).x = g.x * h.x by VALUED_1:5;
    x in dom f by A9, A10, XBOOLE_0:def 4;
    then
A7: h.x <> {} by A0, A8, FUNCT_1:def 9;
    thus (f/"g).x = f.x / g.x by VALUED_1:17
    .= (f.x * h.x) / (g.x * h.x) by A7, XCMPLX_1:91
    .= ((f(#)h)/"(g(#)h)).x by A5, A6, VALUED_1:17;
  end;
  
registration
  let f be differentiable Function of REAL,REAL;
  let g be non-empty differentiable Function of REAL,REAL;
  cluster f /" g -> differentiable for Function of REAL,REAL;
  coherence
  proof
    let h be Function of REAL,REAL such that
A1: h = f /" g;
A2: f /" g = f / g by T2;
    let r;
A3: f is_differentiable_in r & g is_differentiable_in r by POLYDIFF:def 2;
    g.r <> 0 by T4;
    hence thesis by A1,A2,A3,FDIFF_2:14;
  end;  
end;

registration
  let f be non-empty differentiable Function of REAL,REAL;
  cluster f" -> differentiable for Function of REAL,REAL;
  coherence
  proof
    let g be Function of REAL,REAL such that
A1: g = f";
    let r;
A2: f.r <> 0 by T4;
    f is_differentiable_in r by POLYDIFF:def 2;
    then f^ is_differentiable_in r by A2, FDIFF_2:15;
    hence g is_differentiable_in r by A1, T2a;
  end;
end;

theorem T5:
  for f being non-empty differentiable Function of REAL,REAL holds
  f" `| = -(f `|)/"(f^2)
  proof
    let f be non-empty differentiable Function of REAL,REAL;
    let r be Element of REAL;
A1: f" = f^ by T2a;
A2: f.r <> 0 by T4;
A3: f is_differentiable_in r by POLYDIFF:9;
A4: diff(f,r) = (f `|).r by POLYDIFF:10;
A5: (f.r)^2 = (f^2).r by VALUED_1:11;
    thus (f" `|).r = diff(f",r) by POLYDIFF:10
    .= -diff(f,r)/(f.r)^2 by A1, A2, A3, FDIFF_2:15
    .= -(((f `|)/"(f^2)).r) by A4, A5, VALUED_1:17
    .= (-(f `|)/"(f^2)).r by VALUED_1:8;
  end;

theorem
  for f being differentiable Function of REAL,REAL
  for g being non-empty differentiable Function of REAL,REAL holds
  (f /" g) `| = ((f`|)(#)g - f(#)(g`|)) /" (g^2)
  proof
    let f be differentiable Function of REAL,REAL;
    let g be non-empty differentiable Function of REAL,REAL;
A1: ((f `|)(#)g) /" (g^2) = ((f `|)(#)g) / (g^2) by T2;
A2: (f(#)(g `|))/"(g^2) = (f(#)(g `|))/(g^2) by T2;
A3: ((f `|)(#)g-f(#)(g `|))/"(g^2) = ((f `|)(#)g-f(#)(g `|))/(g^2) by T2;
A4: dom(f `|) = REAL & dom(g) = REAL by FUNCT_2:def 1;
A6: (f `|) /" g = ((f `|)(#)g)/"(g(#)g) by A4, T6;
    thus (f /" g) `| = g"(#)(f`|) + f(#)((g") `|) by POLYDIFF:16
    .= (f `|) /" g + f(#) -((g `|)/"(g^2)) by T5
    .= (f `|) /" g + -((g `|)/"(g^2)(#)f) by VALUED_2:25
    .= (f `|) /" g - f(#)(g `|)/"(g^2) by VALUED_2:20
    .= ((f `|)(#)g - f(#)(g `|)) /" (g^2) by A1, A2, A3, A6, RFUNCT_1:39;
  end;

theorem T1:
  exp_R".r = 1/(exp_R.r) by VALUED_1:10;

registration
  cluster exp_R -> differentiable;
  coherence by SIN_COS:65;
end;

registration
  cluster exp_R -> non-empty;
  coherence
  proof
    let x be object;
    assume x in dom exp_R;
    hence thesis by SIN_COS:54;
  end;
end;

registration
  cluster exp_R" -> differentiable for Function of REAL,REAL;
  coherence;
end;

begin

set e = number_e;

reserve c for Element of F_Complex;
reserve p for Polynomial of F_Real;

:: definition
::   let f be FinSequence;
::   attr f is R_polynomial_yielding means
::   for n st 1 <= n <= len f holds f.n is Polynomial of F_Real;
:: end;

:: registration
::   cluster R_polynomial_yielding for FinSequence;
::   existence;
:: end;

definition
  let p be Polynomial of F_Real;
  set N = len(p)+1;
  set D = the carrier of Polynom-Ring F_Real;
  reconsider p1 = p as Element of D by POLYNOM3:def 10;
  defpred P[object,object,object] means
  for q being Polynomial of F_Real st q = $2 holds $3 = poly_diff(q);
  func diff_seq(p) -> FinSequence of Polynom-Ring F_Real means :Def1:
  len(it) = len(p)+1 & it.1 = p &
  for n st 1 <= n < len(it) holds
   for q being Polynomial of F_Real st q = it.n holds it.(n+1) = poly_diff(q);
  existence
  proof
A1: for n being Nat st 1 <= n & n < N for x being Element of D
    ex y being Element of D st P[n,x,y]
    proof
      let n;
      assume 1 <= n & n < N;
      let x be Element of D;
      reconsider y = poly_diff(x) as Element of D by POLYNOM3:def 10;
      take y;
      thus thesis;
    end;
    consider F being FinSequence of D such that
A2: len F = N and
A3: F.1 = p1 or N = 0 and
A4: for n st 1 <= n & n < N holds P[n,F.n,F.(n+1)]
    from RECDEF_1:sch 4(A1);
    take F;
    thus len(F) = len(p)+1 by A2;
    thus F.1 = p by A3;
    thus thesis by A2, A4;
  end;
  uniqueness
  proof
    let F,G be FinSequence of Polynom-Ring F_Real such that
A5: len(F) = N and
A6: F.1 = p and
A7: for n st 1 <= n < len(F) holds
    for q being Polynomial of F_Real st q = F.n holds F.(n+1) = poly_diff(q)
    and
A8: len(G) = N and
A9: G.1 = p and
A10: for n st 1 <= n < len(G) holds
    for q being Polynomial of F_Real st q = G.n holds G.(n+1) = poly_diff(q);
A11: for n st 1 <= n & n < N for x,y1,y2 being Element of D st
    P[n,x,y1] & P[n,x,y2] holds y1 = y2
    proof
      let n;
      assume 1 <= n & n < N;
      let x,y1,y2 be Element of D;
      assume P[n,x,y1];
      then y1 = poly_diff(x);
      hence thesis;
    end;
A12: len F = N & (F.1 = p1 or N = 0) &
    for n st 1 <= n & n < N holds P[n,F.n,F.(n+1)] by A5,A6,A7;
A13: len G = N & (G.1 = p1 or N = 0) &
    for n st 1 <= n & n < N holds P[n,G.n,G.(n+1)] by A8,A9,A10;
    thus F = G from RECDEF_1:sch 8(A11,A12,A13);
  end;
end;

definition
  let S be FinSequence of Polynom-Ring F_Real;
  set N = len(S);
  set D = the carrier of Polynom-Ring F_Real;
  defpred P[Nat,object,object] means
  for p, q being Polynomial of F_Real st p = S.($1+1) & q = $2 holds
   $3 = p + q;
  func Partial_Sums(S) -> FinSequence of Polynom-Ring F_Real means :Def2:
  len(it) = len(S) & it.1 = S.1 &
  for n st 1 <= n < len(it) holds
   for p, q being Polynomial of F_Real st p = S.(n+1) & q = it.n holds
    it.(n+1) = p + q if S <> {}
  otherwise it = {};
  existence
  proof
    thus S <> {} implies ex F being FinSequence of Polynom-Ring F_Real st
     len(F) = len(S) &
     F.1 = S.1 & for n st 1 <= n < len(F) holds
     for p, q being Polynomial of F_Real st p = S.(n+1) & q = F.n holds
     F.(n+1) = p + q
    proof
      assume
A1:   S <> {};
      then rng S <> {};
      then 1 in dom S by FINSEQ_3:32;
      then S.1 in rng S by FUNCT_1:3;
      then reconsider p1 = S.1 as Element of D;
A2:    for n being Nat st 1 <= n & n < N for x being Element of D
      ex y being Element of D st P[n,x,y]
      proof
        let n;
        assume that 1 <= n and
A3:     n < N;
        let x be Element of D;
        n+1 <= N by A3,NAT_1:13;
        then n+1 in dom S by NAT_1:12,FINSEQ_3:25;
        then S.(n+1) in rng S by FUNCT_1:3;
        then reconsider p = S.(n+1) as Polynomial of F_Real
        by POLYNOM3:def 10;
        reconsider y = p + x as Element of D by POLYNOM3:def 10;
        take y;
        thus thesis;
      end;
      consider F being FinSequence of D such that
A4:   len F = N and
A5:   F.1 = p1 or N = 0 and
A6:   for n st 1 <= n & n < N holds P[n,F.n,F.(n+1)]
      from RECDEF_1:sch 4(A2);
      take F;
      thus len(F) = len(S) by A4;
      thus F.1 = S.1 by A1,A5,FINSEQ_1:20;
      thus thesis by A4, A6;
    end;
    thus thesis;
  end;
  uniqueness
  proof
    let F,G be FinSequence of Polynom-Ring F_Real;
    thus S <> {} &
    len(F) = N & F.1 = S.1 & (for n st 1 <= n < len(F) holds
    for p, q being Polynomial of F_Real st p = S.(n+1) & q = F.n holds
    F.(n+1) = p + q) &
    len(G) = N & G.1 = S.1 & (for n st 1 <= n < len(G) holds
    for p, q being Polynomial of F_Real st p = S.(n+1) & q = G.n holds
    G.(n+1) = p + q)
    implies F = G
    proof
      assume that
A7:   S <> {} and
A8:   len(F) = N and
A9:   F.1 = S.1 and
A10:  for n st 1 <= n < len(F) holds
      for p, q being Polynomial of F_Real st p = S.(n+1) & q = F.n holds
      F.(n+1) = p + q
      and
A11:  len(G) = N and
A12:  G.1 = S.1 and
A13:  for n st 1 <= n < len(G) holds
      for p, q being Polynomial of F_Real st p = S.(n+1) & q = G.n holds
      G.(n+1) = p + q;
A14:  for n st 1 <= n & n < N for x,y1,y2 being Element of D st
      P[n,x,y1] & P[n,x,y2] holds y1 = y2
      proof
        let n;
        assume that 1 <= n and
A15:    n < N;
        let x,y1,y2 be Element of D;
        n+1 <= N by A15,NAT_1:13;
        then n+1 in dom S by NAT_1:12,FINSEQ_3:25;
        then S.(n+1) in rng S by FUNCT_1:3;
        then reconsider p = S.(n+1) as Polynomial of F_Real
        by POLYNOM3:def 10;
        assume P[n,x,y1];
        then y1 = p + x;
        hence thesis;
      end;
      rng S <> {} by A7;
      then 1 in dom S by FINSEQ_3:32;
      then S.1 in rng S by FUNCT_1:3;
      then reconsider p1 = S.1 as Element of D;
A16:  len F = N & (F.1 = p1 or N = 0) &
      for n st 1 <= n & n < N holds P[n,F.n,F.(n+1)] by A8,A9,A10;
A17:  len G = N & (G.1 = p1 or N = 0) &
      for n st 1 <= n & n < N holds P[n,G.n,G.(n+1)] by A11,A12,A13;
      thus F = G from RECDEF_1:sch 8(A14,A16,A17);
    end;
    thus thesis;
  end;
  consistency;
end;

definition
  let S be FinSequence of Polynom-Ring F_Real;
  func Sum(S) -> Polynomial of F_Real equals
  Partial_Sums(S).len(Partial_Sums(S)) if S <> {}
  otherwise 0_.F_Real;
  coherence
  proof
    set F = Partial_Sums(S);
    thus S <> {} implies F.len(F) is Polynomial of F_Real
    proof
      assume
A1:   S <> {};
      then
A2:   len F = len S by Def2;
      1 <= len S by A1,FINSEQ_1:20;
      then len F in dom F by A2,FINSEQ_3:25;
      then F.len F in rng F by FUNCT_1:3;
      hence thesis by POLYNOM3:def 10;
    end;
    thus thesis;
  end;
  consistency;  
end;

definition
  let p;
  func Sum_of_diffs(p) -> Polynomial of F_Real equals
  Sum diff_seq(p);
  coherence;
end;

definition
  let p;
  func WFx(p) -> Function of REAL,REAL equals
  exp_R" (#) Eval Sum_of_diffs(p);
  coherence;
end;

theorem
  WFx(p).r = 1/(exp_R.r) * (Eval Sum_of_diffs(p)).r
  proof
    exp_R".r = 1/(exp_R.r) by VALUED_1:10;
    hence thesis by VALUED_1:5;
  end;

registration
  let p;
  cluster WFx(p) -> differentiable;
  coherence;
end;

theorem
  (WFx(p)) `| = - (exp_R") (#) Eval p;
::>                                 *4

registration
  cluster number_e -> transcendental;
  coherence
  proof
    e in COMPLEX by XCMPLX_0:def 2;
    then reconsider e as Element of F_Complex by COMPLFLD:def 1;
    assume number_e is algebraic;
    then consider f being non-zero INT -valued Polynomial of F_Complex
    such that
A1: e is_a_root_of f by LIOUVIL2:25;
    thus contradiction;
::>                  *1
  end;
end;

::> 1: It is not true
::> 4: This inference is not accepted
